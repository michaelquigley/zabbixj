apply plugin: 'maven'

dependencies {
	compile 'org.json:json:20090211'
}

task bundlePom << {
    pom {
        project {
            name 'Zabbix/J'
            description 'Zabbix/J is a framework for integrating Zabbix metrics support into Java applications.'
            url 'http://quigley.com/'
            licenses {
                license {
                    name 'Apache License Version 2.0'
                    url 'http://www.apache.org/licenses/'
                    distribution 'repo'
                }
            }
            scm {
                connection 'scm:git:https://github.com/michaelquigley/zabbixj.git'
                url 'https://github.com/michaelquigley/zabbixj.git'
            }
            developers {
                developer {
                    name 'Michael Quigley'
                    email 'michael@quigley.com'
                }
            }
        }
    }.writeTo('build/libs/zabbixj-' + version + '.pom')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}